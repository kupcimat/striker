buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'org.saigon'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 10

springBoot {
    buildInfo()
}

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')

    // required for hibernate, because it was removed in java 10
    runtime('javax.xml.bind:jaxb-api:2.3.0')
    runtime('org.flywaydb:flyway-core')
    runtime('org.postgresql:postgresql')
    runtime('com.h2database:h2')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.boot:spring-boot-starter-webflux')
}

test {
    def serverUrl = System.getProperty("serverUrl")
    if (serverUrl) {
        systemProperty "serverUrl", serverUrl
        filter.includeTestsMatching "*IT"
    } else {
        filter.includeTestsMatching "*Test"
    }

    testLogging.events "passed", "skipped", "failed"
    testLogging.exceptionFormat "full"
}

// task run by heroku
task stage {
    dependsOn clean
    dependsOn bootJar
}
bootJar.mustRunAfter clean
